HAI 1.3
    BTW STARTN STALIN SORT FUNCTION
    HOW IZ I STALIN_SORT YR IN_LIST AN YR LIST_LEN
        I HAS A OUT_LIST ITZ A BUKKIT
        I HAS A OUT_LIST_LEN ITZ 1
        I HAS A LAST_NUM ITZ IN_LIST'Z SRS 0
        OUT_LIST HAS A SRS 0 ITZ LAST_NUM
        LIST_LEN R DIFF OF LIST_LEN AN 1
        
        IM IN YR LOOP UPPIN YR INDEX TIL BOTH SAEM INDEX LIST_LEN
            BOTH SAEM LAST_NUM AN SMALLR OF LAST_NUM AN IN_LIST'Z SRS SUM OF INDEX AN 1, O RLY?
                YA RLY
                    OUT_LIST HAS A SRS OUT_LIST_LEN ITZ IN_LIST'Z SRS SUM OF INDEX AN 1
                    LAST_NUM R OUT_LIST'Z SRS OUT_LIST_LEN
                    OUT_LIST_LEN R SUM OF OUT_LIST_LEN AN 1
            OIC
        IM OUTTA YR LOOP
        
        I HAS A RET_VAL ITZ A BUKKIT
        RET_VAL HAS A NEW_LIST_LEN ITZ OUT_LIST_LEN
        RET_VAL HAS A NEW_LIST ITZ OUT_LIST
        FOUND YR RET_VAL
    IF U SAY SO
    BTW END OF STALIN SORT FUNCTION

    BTW CREATING LIST TO BE SORTED
    BTW USING USER INPUT FOR NUMBR TYPE TENDS TO LEAD TO FUNKY BEHAVIOR, SO IT WAS IGNORED FOR NOW
    I HAS A LIST_LENGTH ITZ 7
    I HAS A NOSORT_LIST ITZ A BUKKIT
    NOSORT_LIST HAS A SRS 0 ITZ 6
    NOSORT_LIST HAS A SRS 1 ITZ 6
    NOSORT_LIST HAS A SRS 2 ITZ 7
    NOSORT_LIST HAS A SRS 3 ITZ 5
    NOSORT_LIST HAS A SRS 4 ITZ 3
    NOSORT_LIST HAS A SRS 5 ITZ 0
    NOSORT_LIST HAS A SRS 6 ITZ 9

    BTW GETTING SORTED LIST
    I HAS A RETURNED_VALUE ITZ I IZ STALIN_SORT YR NOSORT_LIST AN YR LIST_LENGTH MKAY
    I HAS A SORT_LIST_LENGTH ITZ RETURNED_VALUE'Z NEW_LIST_LEN
    I HAS A SORT_LIST ITZ RETURNED_VALUE'Z NEW_LIST
    
    BTW SHOWING THE RESULTS
    VISIBLE "LIST SORTED VIA STALIN SORT:"
    IM IN YR LOOP UPPIN YR INDEX TIL BOTH SAEM INDEX SORT_LIST_LENGTH
        VISIBLE SORT_LIST'Z SRS INDEX
    IM OUTTA YR LOOP

KTHXBYE